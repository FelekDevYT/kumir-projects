
использовать Рисователь
использовать Клавиатура
целтаб координаты[1:2]
литтаб инвентарь[1:5]
целтаб блоки [1:16]
литтаб имя_блоков[1:16]
литтаб цвета_блоков[1:16]
цвет color_Of_Block
цел current_position_in_inventory
|1 - cobblestone(darck)
|2 - air(blue)
|3 - grass(green)
|4 - snow(white)
|5 - iron(gray)
|6 - gold(yellow)
|7 - diamond(light-blue)
алг
нач
Starter
кон

алг Starter
нач
новый лист(640,320,RGB(124, 207, 243))
Initialisation
Generation
Custon_generation
цел с
нц для с от 1 до 5
инвентарь[с] := "| Null |"
кц
координаты[1] := 101
координаты[2] := 100
вывод "[UserInfo] ",инвентарь[1],инвентарь[2],инвентарь[3],инвентарь[4],инвентарь[5],нс
Player
сканкод к
нц
к:=код клав
RefactorPosition(к)
кц
кон

алг Initialisation
нач
Colorer("green")
current_position_in_inventory := 0
цел а
нц для а от 8 до 16
имя_блоков[а] := "null"
кц
имя_блоков[1]:="| cobblestone |"
имя_блоков[2]:="| air |"
имя_блоков[3]:="| grass |"
имя_блоков[4]:="| snow |"
имя_блоков[5]:="| iron |"
имя_блоков[6]:="| gold |"
имя_блоков[7]:="| diamond |"
цвета_блоков[1]:="black"
цвета_блоков[2]:="light-blue"
цвета_блоков[3]:="green"
цвета_блоков[4]:="white"
цвета_блоков[5]:="light-black"
цвета_блоков[6]:="yellow"
цвета_блоков[7]:="diamond-blue"
кон

алг Colorer(лит строка)
нач
выбор
при строка = "black":color_Of_Block:=черный
при строка = "light-blue":color_Of_Block:=RGB(124, 207, 243)
при строка = "green":color_Of_Block:=зеленый
при строка = "white":color_Of_Block:=белый
при строка = "light-black":color_Of_Block:=RGB(132, 141, 148)
при строка = "yellow":color_Of_Block:=желтый
при строка = "diamond-blue":color_Of_Block:=RGB(54, 171, 186)
при строка = "red":color_Of_Block:=красный
при строка = "blue":color_Of_Block:=синий
при строка = "old-diamond-blue":color_Of_Block:=голубой
все
кон

алг Custon_generation
нач
цел x,y
|Generate plains
x:=1
y:=300
нц 20 раз 
нц 6 раз 
Cube(x,y,RGB(124, 207, 243))
x:=x+20
кц
x:=1
y:=y-20
кц
x:=1
y:=300
нц 6 раз 
нц irand(6,7) раз
Cube(x,y,зеленый)
y:=y-20
кц
y:=300
x:=x+20
кц
кон

алг Generation
нач
целтаб высота[1:32]
цел разок,пр
пр:=0
нц для разок от 1 до 32
высота[разок] := irand(1,12)
кц
цел x,y,разик,тв
x:=1
y:=300
тв:=1
цвет colorOfBlock
нц для разик от 1 до 32
нц высота[разик] раз
если тв >= 1
то
colorOfBlock := черный
все
если тв >= irand(6,10)
то
colorOfBlock := зеленый
все
если тв >= irand(10,12)
то
colorOfBlock:= белый
все
выбор
при тв <=8 и irand(1,5) = irand(1,3) и irand(1,2) = irand(1,2):colorOfBlock:=RGB(132, 141, 148)
при тв <=6 и irand(1,7) = irand(1,5) и irand(1,2) = irand(1,3):colorOfBlock:=желтый
при тв <=4 и irand(1,12) = irand(1,2) и irand(1,3) = irand(1,5):colorOfBlock:=RGB(54, 171, 186)
все
Cube(x,y,colorOfBlock)
y:=y-20
тв:=тв+1
кц
тв:=1
x:=x+20
y:=300
кц
кон

алг Cube(цел startX,цел startY,цвет а) 
нач 
перо(5,а)
линия(startX,startY,startX+20,startY)
линия(startX+20,startY,startX+20,startY+20)
линия(startX+20,startY+20,startX,startY+20)
линия(startX,startY+20,startX,startY)
кон

алг RefactorPosition(сканкод кл)
нач
лит команда
цел current_block
выбор
при кл = КЛ_W:
Cube(координаты[1],координаты[2],RGB(124, 207, 243))
Cube(координаты[1],координаты[2]-20,красный)
координаты[2] := координаты[2]-20
при кл = КЛ_S:
Cube(координаты[1],координаты[2],RGB(124, 207, 243))
Cube(координаты[1],координаты[2]+20,красный)
координаты[2] := координаты[2]+20
при кл = КЛ_A:
Cube(координаты[1],координаты[2],RGB(124, 207, 243))
Cube(координаты[1]-20,координаты[2],красный)
координаты[1] := координаты[1]-20
при кл = КЛ_D:
Cube(координаты[1],координаты[2],RGB(124, 207, 243))
Cube(координаты[1]+20,координаты[2],красный)
координаты[1] := координаты[1]+20
при кл = КЛ_ВВЕРХ:
Cube(координаты[1],координаты[2]-20,color_Of_Block)
при кл = КЛ_ВНИЗ:
Cube(координаты[1],координаты[2]+20,color_Of_Block)
при кл = КЛ_ВПРАВО:
Cube(координаты[1]+20,координаты[2],color_Of_Block)
при кл = КЛ_ВЛЕВО:
Cube(координаты[1]-20,координаты[2],color_Of_Block)
при кл = КЛ_T:
команда := ""
вывод "Enter command>"
ввод команда,нс
если команда = "give"
то
вывод "Enter block name>"
ввод current_block,нс
BlockStatement(current_block)
вывод "[UserInfo] ",инвентарь[1],инвентарь[2],инвентарь[3],инвентарь[4],инвентарь[5],нс
все 
при кл = КЛ_M:
сохранить лист("terrain.png")
все
кон

алг Player
нач
Cube(координаты[1],координаты[2],красный)
кон

алг BlockStatement(цел name_block)
нач
current_position_in_inventory:=current_position_in_inventory+1
инвентарь[current_position_in_inventory]:=имя_блоков[name_block]
кон

|||
|||MODIFICATION, CODER PACK,MORE
|||  
